# Cap칤tulo 8: ERC721: takeOwnership

Genial. 춰Ahora terminemos nuestra implementaci칩n de ERC721 con la 칰ltima funci칩n! (No te preocupes, todav칤a hay m치s por cubrir en la Lecci칩n 5 despu칠s de esto 游땔)

La 칰ltima funci칩n, **takeOwnership**, simplemente debe verificar que **msg.sender** ha sido aprobado para tomar este token / zombie, y llamar a **_transfer** si es as칤.

# Vamos a probarlo

1. Primero, queremos usar la declaraci칩n require para verificar que zombieApprovals para _tokenId es igual a msg.sender.

De esta forma, si msg.sender no ha sido aprobado para tomar este token, lanzar치 un error.

2. Para llamar a _transfer, necesitamos saber la direcci칩n de la persona que posee el token (requiere _from como par치metro). Afortunadamente, podemos buscar esto con nuestra funci칩n ownerOf.

As칤 que, declare una variable tipo address llamada owner, ponga como valor el mismo que devuelve ownerOf(_tokenId).

3. Finalmente, llame a _transfer, y pase toda la informaci칩n necesaria. (Aqu칤 puede usar msg.sender para el valor _to, ya que la persona que llama a esta funci칩n es a la que debe enviarse el token).

_Nota: Podr칤amos haber hecho los pasos 2 y 3 en una l칤nea de c칩digo, pero dividirla hace que las cosas sean un poco m치s legibles. Preferencia personal._

```s
pragma solidity ^0.4.19;

import "./zombieattack.sol";
import "./erc721.sol";

contract ZombieOwnership is ZombieAttack, ERC721 {

  mapping (uint => address) zombieApprovals;

  function balanceOf(address _owner) public view returns (uint256 _balance) {
    return ownerZombieCount[_owner];
  }

  function ownerOf(uint256 _tokenId) public view returns (address _owner) {
    return zombieToOwner[_tokenId];
  }

  function _transfer(address _from, address _to, uint256 _tokenId) private {
    ownerZombieCount[_to]++;
    ownerZombieCount[_from]--;
    zombieToOwner[_tokenId] = _to;
    Transfer(_from, _to, _tokenId);
  }

  function transfer(address _to, uint256 _tokenId) public onlyOwnerOf(_tokenId) {
    _transfer(msg.sender, _to, _tokenId);
  }

  function approve(address _to, uint256 _tokenId) public onlyOwnerOf(_tokenId) {
    zombieApprovals[_tokenId] = _to;
    Approval(msg.sender, _to, _tokenId);
  }

  function takeOwnership(uint256 _tokenId) public {
    require(zombieApprovals[_tokenId] == msg.sender);
    address owner = ownerOf(_tokenId);
    _transfer(owner, msg.sender, _tokenId);

  }
}
```